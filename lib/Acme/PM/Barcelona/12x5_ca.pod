=head1 NOM

Acme::PM::Barcelona::12x5_ca - 12 xerrades, 5 minuts cadascuna

=head1 VERSIE<Oacute>

Version 0.01

=head1 SINOPSI

IntroducciE<oacute> rE<agrave>pida a Perl a cE<agrave>rrec dels grup
dels Perl Mongers de Barcelona.

=head1 DESCRIPCIE<Oacute>

12x5 E<eacute>s un format de presentaciE<oacute> que en Mark Jason Dominus
va mostrar al grup de Barcelona.pm el juliol de 2003. Ens va agradar
tant que volem fer el mateix per introduir tant Perl com la nostra
comunitat local de Perl a qualsevol que ens escolti.

=head1 PRESENTACIONS

=head2 Perl i els Perl Mongers

=head3 QuE<egrave> E<eacute>s Perl?

L<http://www.perl.org/>

Perl E<eacute>s un llenguatge de programaciE<oacute> dinE<agrave>mic
que va dissenyar un lingE<uuml>ista anomenat Larry Wall l'any 1987.

Des d'aleshores enE<ccedil>E<agrave> s'han publicat diverses versions
amb canvis importants en el llenguatge, essent la versiE<oacute> 5
la mE<eacute>s utilitzada fins ara. La versiE<oacute> 6 incorporara
tants canvis que es considera un llenguatge diferent, Perl 6.

=head3 Per quE<egrave> Perl?

BE<agrave>sicament perquE<egrave> E<eacute>s el llenguatge de
programaciE<oacute> que a mi mE<eacute>s m'agrada. Hi ha gent
que en prefereix d'altres perE<ograve>, E<eacute>s clar,
hi ha d'haver gent per tot en aquest mE<oacute>n.

I a mi, per quE<egrave> m'agrada tant?

=over 4

=item * E<eacute>s divertit d'utilitzar i aprendre'l

=item * hi ha molt de codi ja fet i disponible lliurement

=item * la comunitat de Perl E<eacute>s gran i estE<agrave> disposada a ajudar

=item * estE<agrave> disponible en molts mE<eacute>s llocs del que us penseu

=item * hi puc fer coses que no podria amb d'altres llenguatges

=item * em permet fer les coses com m'agradin mE<eacute>s

=item * m'ajuda a fer la feina

=back

=head3 QuE<egrave> sE<oacute>n els Perl Mongers?

Perl Mongers E<eacute>s una mena associaciE<oacute> internacional
de tots els grups d'usuaris de Perl.

Cada grup pot fer coses molt diferents del que fan els altres,
perE<ograve> en la majoria de casos es tracta de grups d'usuaris
d'un lloc determinat que es reuneixen periE<ograve>dicament per
parlar de Perl i d'altres temes relacionats.

=head3 Barcelona.pm, els Perl Mongers de Barcelona

L<http://barcelona.perl.org/>

Creat a principis de l'any 2003 per en Timothy Chen Allen,
el grup de mongers de Barcelona es reuneix el darrer dijous
de cada mes.

De vegades, des del grup tambE<eacute> s'organitzen festivals,
xerrades i cursos de Perl amb aprofitant el pas per Barcelona
d'alguns gurE<uacute>s de la comunitat internacional.

=head2 WWW::Mechanize

Amb Perl es poden escriure programes que naveguen per la web.

=head3 La pE<agrave>gina d'inici de La Vanguardia

Aquest programa descarrega la pE<agrave>gina web de La Vanguardia.

  use WWW::Mechanize;
  
  $mech = WWW::Mechanize->new;
  $mech->get("http://www.lavanguardia.es");
  
  print $mech->content;

=head3 Una cerca a Google

Aquest programa cerca la paraula "Perl" a Google.

  use WWW::Mechanize;
  
  my $mech = WWW::Mechanize->new;
  
  $mech->get('http://www.google.com');
  $mech->field('q', 'Perl');
  $mech->click('btnG');
  
  print $mech->content;

=head3 L'extracte d'un compte de La Caixa

Aquest programa treu per pantalla la primera pE<agrave>gina de l'extracte
d'un compte de La Caixa.

  use WWW::Mechanize;
  
  $user = 'el-teu-dni';
  $password = 'contrasenya';
  
  # fest-te passar per un iPhone, es mes senzill
  $iphone = "Mozilla/5.0 (iPhone; U; CPU iPhone OS 3_0 like Mac OS X; en-us) AppleWebKit/528.18 (KHTML, like Gecko) Version/4.0 Mobile/7A341 Safari/528.16";
  $mech = WWW::Mechanize->new(agent => $iphone);
  
  # home page
  $mech->get("http://lacaixa.mobi/apl/index_es.html?select=true");
  $mech->follow_link(url_regex => qr/es-mobi-menu-lo/);
  
  # fes login
  $mech->submit_form(
  	form_name => "formu",
  	fields => {
  		E => $user,
  		B => $password
  	}
  );

=head3 ...

  # segueix el primer enllac,	
  $mech->follow_link(tag => 'a', n => 1);	
  
  # captura els moviments
  ($movements) = $mech->content =~ m{(<ul[^>]*>\s*<li[^>]*>\s*<a.*?</ul>)}i;
  
  $mov_regexp = qr{
      <td\s+align="left"> (.+?) </td>
      .*?
      <font[^>]+> ([\d,-]+) .*? </font>
      .*?
      <li[^>]+> (\d+/\d+/\d+)
  }sx;
  
  print "$1, $2, $3\n" while $movements =~ /$mov_regexp/g;
  
  # AHORRA EL CAMBIO, -2,10, 06/09/2009
  # FRANKFURT PLATJA-, -14,40, 05/09/2009
  # DECATHLON ILLA PR, -7,90, 05/09/2009
  # CAPRABO, -7,64, 04/09/2009
  # GESOIL S.A. LEVAN, -18,88, 04/09/2009
  # ...

=head2 Finance::Quote i Cache::FileCache

Es pot obtenir el canvi actualitzat de la moneda d'arreu del mE<oacute>n.

=head3 ConversiE<oacute> de dE<ograve>lars a euros

  use Finance::Quote 1.16;

  $quote = Finance::Quote->new();
  $ratio = $quote->currency('USD', 'EUR');

  print "10 USD = ", 10 * $ratio, " EUR\n";

=head3 Millorem la velocitat amb una cE<ograve>pia temporal

  use Cache::FileCache;
  use Finance::Quote 1.16;

  $cache = Cache::FileCache->new({
    default_expires_in => '1 day',
  });

  $ratio = $cache->get("USD:EUR");
  if ( !defined $ratio ) {
    $quote = Finance::Quote->new();
    $ratio = $quote->currency('USD', 'EUR')
    $cache->set("USD:EUR", $ratio);
  }

  print "10 USD = ", 10 * $ratio, " EUR\n";

=head2 Flickr::Upload

Una eina que utilitza C<Flickr::API> per a publicar fotos a Flickr.

=head3 flickr_upload

La manera mE<eacute>s fE<agrave>cil d'utilitzar l'eina E<eacute>s amb
l'ordre C<flickr_upload> des de l'intE<egrave>rpret d'ordres. Primer
heu de configurar l'accE<eacute>s al vostre compte de Flickr seguint
les instruccions que us indicarE<agrave> aquesta ordre:

  $ flickr_upload --auth

I un cop ho haureu fet, ja podreu publicar les fotos que vulgueu:

  $ flickr_upload --progress --tag 'barcelona perl mongers' *.jpg

=head3 Programar amb Flickr::Upload

PerE<ograve> tambE<eacute> es pot utilitzar des d'un programa que
permeti triar amb mE<eacute>s flexibilitat com voleu etiquetar
les vostres fotos.

  use Flickr::Upload;

  $ua = Flickr::Upload->new({
    'key'    => $key,
    'secret' => $secret,
  });

  $ua->upload(
    'auth_token' => $auth_token,
    'photo'      => 'barcelona-pm.jpg',
    'tags'       => 'barcelona perl mongers',
    'is_public'  => 1,
    'is_friend'  => 1,
    'is_family'  => 1,
  );

=head2 Codis QR

=head3 IntroducciE<oacute>

Els codis QR s'utilitzen per a codificar informaciE<oacute> que pugui
ser llegida i utilitzada de forma rE<agrave>pida des d'un dispositiu
lector.

Es tracta d'un invent japonE<agrave>s per tal que es pugui llegir
informaciE<oacute> rE<agrave>pidament des dels mE<ograve>bils i
es pugui utilitzar d'immediat sense haver d'escriure res, per exemple
posant-ho en un anunci d'un diari o en una pancarta al carrer, etc.

=head3 Tipus de dades

S'assemblen als codis de barres perE<ograve> permeten emmagatzemar
tot tipus de dades:

=over 4

=item * numE<egrave>riques

=item * alfanumE<egrave>riques

=item * binE<agrave>ries

=item * kanji o kana

=back

=head3 CorrecciE<oacute> d'errors

Una altra caracterE<iacute>stica interessant dels codis QR E<eacute>s
que permeten fer correcciE<oacute> d'errors si el dibuix no es veu
del tot bE<eacute>. N'hi ha de 4 nivells:

=over 4

=item L recupera el 7%

=item M recupera el 15%

=item Q recupera el 25%

=item H recupera el 30%

=back

=head3 GD::Barcode::QRcode

Generar un codi QR E<eacute>s molt senzill:

  use GD::Barcode::QRcode;

  binmode(STDOUT);
  print GD::Barcode::QRcode->new('http://perl.cat/')->plot->png;

=head3 Image::DecodeQR

Llegir un codi QR E<eacute>s encara mE<eacute>s senzill:

  use Image::DecodeQR;

  $url = Image::DecodeQR::decode('perl.cat_qr.png');

=head2 TE<iacute>tol6

=head2 TE<iacute>tol7

=head2 TE<iacute>tol8

=head2 TE<iacute>tol9

=head2 TE<iacute>tol10

=head2 Cultura Perl

=head3 Perl Golf

=over 4

=item * El Perl Golf E<eacute>s un divertiment.

=item * Consisteix en resoldre un cert problema en Perl usant el mE<iacute>nim
nombre de carE<agrave>cters possible.

=item * El nom es una conya que ve de l'anglE<egrave>s. Es tracta d'usar el
mE<iacute>nim nombre de B<(key)strokes>. Seguint amb l'analogia, als
problemes en golf se'ls anomena B<holes>.

=item * Normalment es juga a golf en torneigs.

=back

=head3 Exemple 1

Redueix un nombre enter al seu nombre secret:

  #!/usr/bin/perl -l
  print while+s/./hex($`%10+$&)%15/eg^s//pop/e

=head3 Exemple 2

Troba tots els anagrames:

  #!/usr/bin/perl
  map!s/
  \b/ /|//||print,sort%%for map$%.=$_,sort<>

=head3 Exemple 3

Fes una ordenaciE<oacute> topologica:

  #!/usr/bin/perl -ln0
  / \Q$&/||print$1while s/(
  |(?= ?\G))\Q$&/
  /g,/(\S+) /g/$#+

=head3 Poesia Perl

=over 4

=item * Com que Perl tE<eacute> paraules clau i funcions que sE<oacute>n paraules angleses,
i a mE<eacute>s la sintaxi E<eacute>s bastant permisiva, hi ha gent que hi escriu poesies.

=item * Les poesies han de ser sintE<agrave>cticament correctes, perE<ograve> es permet que
generin warnings, i no es demana que facin res en particular.

=back

=head3 Exemple 1

  study, write, study,
  do review (each word) if time.
  close book. sleep? what's that?

=head3 Example 2

  no less can I say;
  require strict, close attention
  while you ... write haiku

=head3 Example 3

  APPEAL:
  listen (please, please);
      open yourself, wide;
      join (you, me),
      connect (us,together),
  tell me.
  do something if distressed;
      @dawn, dance;
      @evening, sing;
      read (books,$poems,stories) until peaceful;
      study if able;
      write me if-you-please;
  sort your feelings, reset goals, seek (friends, family, anyone);
          do*not*die (like this)
          if sin abounds;
  keys (hidden), open (locks, doors), tell secrets;
      do not I-beg-you, close them, yet.
                  accept (yourself, changes),
                  bind (grief, despair);
      require truth, goodness if-you-will, each moment;
  select (always), length(of-days)

=head3 JAPHs

=over 4

=item * Un JAPH es un programa Perl que treu per pantalla la frase
"Just another Perl hacker,".

=item * Normalment els JAPHs sE<oacute>n molt originals, o molt
obfuscats.

=back

=head3 Exemple 1

  $_ = "wftedskaebjgdpjgidbsmnjgc";
  tr/a-z/oh, turtleneck Phrase Jar!/; print;

=head3 Exemple 2

  @P=split//,".URRUU\c8R";@d=split//,"\nrekcah xinU / lreP rehtona tsuJ";sub p{
  @p{"r$p","u$p"}=(P,P);pipe"r$p","u$p";++$p;($q*=2)+=$f=!fork;map{$P=$P[$f^ord
  ($p{$_})&6];$p{$_}=/ ^$P/ix?$P:close$_}keys%p}p;p;p;p;p;map{$p{$_}=~/^[P.]/&&
  close$_}%p;wait until$?;map{/^r/&&<$_>}%p;$_=$d[$q];sleep rand(2)if/\S/;print

=head3 Exemple 3

  $_=q;Barcelona Perl Mongers;,$/=y,gaudi,,,;map$,+=(split//)*(-1)**$|++
  ,(split)[.11_09,1.714];$.=''!~m~erce~;$"=y,catalunya,,,$;=y,rambles,,,
  $*=$/^$.;$:=$.+length,$@=$***$**$/**$*%$:,$%=$/*$"-$*;print+chr($_<0xA
  ?$.."$[$_":m:^$.:?$..$_:$_)for($**$**$/*$",$"*$@+$**$/,$**$,,$***$,,$/
  *$,,$;,$***$/,$,*$/,$.<<$,,$%-$*,$"+$/,$***$,,(($,*$*)**$*)-$/,$***$/,
  $@+$/,$:-($/**$*),$.,$:+$"+$*,$.<<$*,$,**$/-$:-$,,exp$:/$,,$",$.,$"*$*
  ,$***$,,log$.,$.,-$/+$"*$,,$/+$"*$;,$.,++$@,$***($/&=$/+Barcelona_pm))

=head2 Perl.cat

=head3 La comunitat de programadors de Perl d'àmbit català

L<http://perl.cat/>

Aquest any 2009 hem comprat el domini I<Perl.cat> i hi hem muntat
un blog i poca cosa mE<eacute>s, perE<ograve> confiem en anar
fent crE<eacute>ixer la comunitat local poc a poc i omplir de
continguts el domini.

Qui s'hi vol apuntar?

=head1 AUTOR

Alex Muntada, C<< <alexm at cpan.org> >>
Xavier Noria, C<< <fxn at cpan.org> >>

=head1 ERRADES

Si us plau, informeu de qualsevol errada o millora a
C<bug-acme-pm-barcelona at rt.cpan.org>, o a travE<eacute>s
de la web L<http://rt.cpan.org/NoAuth/ReportBug.html?Queue=Acme-PM-Barcelona>.
Jo rebrE<eacute> un avE<iacute>s i se us notificarE<agrave>
automE<agrave>ticament dels canvis que jo hi faci.

=head1 RECONEIXEMENTS

=over 4

=item Mark Jason Dominus

=item Barcelona Perl Mongers

=back

=head1 COPYRIGHT I LLICE<Egrave>NCIA

Copyright 2009 Alex Muntada, tots els drets reservats.

Aquest programa E<eacute>s programari lliure; el podeu redistribuir i/o
modificar sota els mateixos termes que el propi Perl.

=cut
